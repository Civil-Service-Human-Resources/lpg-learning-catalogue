plugins {
    id 'org.springframework.boot' version '2.5.0'
    id 'ajk.gradle.elastic' version '0.0.20'
    id "org.sonarqube" version "2.6.2"
}

repositories {
    mavenCentral()
    maven {
        url 'https://artifacts.elastic.co/maven'
    }
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'uk.gov.cslearning'
version = '0.1.0-' + (System.getenv('TRAVIS_COMMIT') ?: 'local')

bootJar {
    archiveFileName = 'learning-catalogue.jar'
    mainClass.set('uk.gov.cslearning.catalogue.Application')
}

dependencies {

    annotationProcessor 'org.projectlombok:lombok:1.18.4'
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-jetty'
    implementation 'org.springframework.boot:spring-boot-starter-jersey'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.3.3.RELEASE'
    implementation group: 'org.springframework.security', name: 'spring-security-jwt', version: '1.1.1.RELEASE'

    compile('org.springframework.data:spring-data-elasticsearch') {
        exclude module: 'log4j-api'
    }
    implementation 'com.google.guava:guava:23.5-jre'
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'org.codehaus.janino:janino:3.0.8'

    implementation 'io.springfox:springfox-swagger2:2.7.0'
    implementation 'io.springfox:springfox-swagger-ui:2.7.0'

    implementation 'commons-io:commons-io:2.6'
    implementation 'com.microsoft.azure:azure-storage:8.0.0'
    implementation 'com.microsoft.azure:azure-storage-spring-boot-starter:2.0.2'

    implementation 'org.apache.tika:tika-core:1.18'
    implementation 'org.apache.tika:tika-parsers:1.18'

    implementation group: 'com.microsoft.azure', name: 'applicationinsights-web-auto', version: '2.5.0'
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-logging-logback', version: '2.5.0'
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-spring-boot-starter', version: '2.5.0'

    testImplementation 'com.google.code.gson:gson:2.8.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    testImplementation 'org.powermock:powermock-api-mockito2:1.7.4'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.4'

    testImplementation 'org.mockito:mockito-core:2.8.47'
    testImplementation 'com.jayway.jsonpath:json-path:2.4.0'

}

compileJava.dependsOn(processResources)

task integrationTest(type: Test) {
    reports {
        html.destination new File('$buildDir/reports/integration-test')
    }

    include '**/*IT.*'

    doFirst {
        startElastic {
            elasticVersion = '6.2.4'
            httpPort = 9998
            transportPort = 9999
            dataDir = file('$buildDir/elastic')
            logsDir = file('$buildDir/elastic/logs')
        }
    }

    doLast {
        stopElastic {
            httpPort = 9998
        }
    }
}

gradle.taskGraph.afterTask { Task task, TaskState taskState ->
    if (task.name == 'integrationTest') {
        stopElastic {
            httpPort = 9998
        }
    }
}

test {
    include '**/*Test.*'
    exclude '**/*IT.*'
}
